/******************************
*  			LEDs			  *
While: ON

RED(0) 		- Sensoring state
GREEN(1) 	- Requesting Father
YELLOW(2)	- Radio Silent
*******************************/

#include "/home/terra/TerraNet_v0.1/terra/TerraNet.defs"

var ushort nodeid = getNodeId();

pktype usrMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2] d32;
end

var usrMsg msgRadio;
var ubyte stat;
var ushort father;

var ushort[10] childID;
var ubyte[10] seq;

if nodeid == 11 then
	father = 1;
	loop do
		var usrMsg rqMsg = await RECEIVE(3);
		emit LED0(TOGGLE);

		// Start topology
		var usrMsg topoMsg;
		topoMsg.source = nodeid;
		topoMsg.target = BROADCAST;
		topoMsg.type = 2;
		emit SEND(topoMsg);
		await SEND_DONE;
		
		emit LED2(TOGGLE);
		await 5s; // non-responsive time
		emit LED2(TOGGLE);
	end
else
	emit LED1(ON);
	loop do

		/******************************
		* REQUEST FATHER
		*******************************/

		loop do
			par/or	do				
				father = 0;
				var usrMsg rqMsg;
				rqMsg.source = nodeid;
				rqMsg.target = BROADCAST;
				rqMsg.type = 3;
				rqMsg.d16[0] = nodeid;
				emit SEND(rqMsg);
				await SEND_DONE(3);

				var usrMsg topMsg = await RECEIVE(2);
	
				father = topMsg.source;
				topMsg.source = nodeid;
				topMsg.target = BROADCAST;	

				emit SEND(topMsg);
				await SEND_DONE(2);
	
				emit LED2(TOGGLE);
				await 5s;
				emit LED2(TOGGLE);
				break;
			with
				//timeout
				await 10s;
			end
		end
	

		/******************************
		* SENSING
		*******************************/
		emit LED0(ON);
		emit LED1(OFF);

		par/or do	
			par/or do
				await CUSTOM_A;
			with
				var usrMsg reqMsg = await RECEIVE(3);
			end
			emit LED0(OFF);
			emit LED1(ON);
		with
			loop do
				emit REQ_TEMP();
				var ushort temp = await TEMP;
				if temp > 530 then
					emit REQ_CUSTOM_A(1);
				end
				await 5s;
			end
		with
			loop do
				var usrMsg topMsg = await RECEIVE(2);
	
				father = topMsg.source;
				topMsg.source = nodeid;
				topMsg.target = BROADCAST;

				emit SEND(topMsg);
				await SEND_DONE(2);
			
				emit LED2(TOGGLE);
				await 5s;
				emit LED2(TOGGLE);
			end
		end
	end
end
