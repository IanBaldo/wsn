#include "/home/terra/TerraNet_v0.1/terra/TerraNet.defs"

var ushort nodeid = getNodeId();

pktype usrMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2] d32;
end

var usrMsg msgRadio;
var ubyte stat;
var ushort father;

var ushort[10] childID;
var ubyte[10] seq;

if nodeid == 11 then
	father = 1;
	loop do
		var usrMsg rqMsg = await RECEIVE(3);
		emit LED0(TOGGLE);

		// Start topology
		var usrMsg topoMsg;
		topoMsg.source = nodeid;
		topoMsg.target = BROADCAST;
		topoMsg.type = 2;
		emit SEND(topoMsg);
		await SEND_DONE;
		
		emit LED2(TOGGLE);
		await 5s; // non-responsive time
		emit LED2(TOGGLE);
	end
else
	loop do

		/******************************
		* REQUEST FATHER
		*******************************/

		loop do
			par/or	do
				father = 0;
				var usrMsg rqMsg;
				rqMsg.source = nodeid;
				rqMsg.target = BROADCAST;
				rqMsg.type = 3;
				rqMsg.d16[0] = nodeid;
				emit SEND(rqMsg);
				await SEND_DONE(3);

				var usrMsg topMsg = await RECEIVE(2);
	
				father = topMsg.source;
				topMsg.source = nodeid;
				topMsg.target = BROADCAST;

				emit LED1(ON);

				emit SEND(topMsg);
				await SEND_DONE(2);
	
				emit LED2(TOGGLE);
				await 5s;
				emit LED2(TOGGLE);
				break;
			with
				//timeout
				await 10s;
			end
		end
	

		/******************************
		* SENSING
		*******************************/
		emit LED0(ON);

		par/or do
			await CUSTOM_A;
			emit LED0(OFF);
			emit LED1(OFF);
		with
			loop do
				emit REQ_TEMP();
				var ushort temp = await TEMP;
				if temp > 530 then
					emit REQ_CUSTOM_A(1);
				end
				await 5s;
			end
		end
	end
end
